apply plugin: 'com.android.application'

android {
    signingConfigs {
        test {
            keyAlias 'key0'
            keyPassword '123456'
            storeFile file('../gzrdemo.jks')
            storePassword '123456'
        }

        production {
            keyAlias 'key0'
            keyPassword '123456'
            storeFile file('../gzrdemo.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.gzr.gzrdemo"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.1.0"
        buildConfigField "boolean", "PRINT_LOG", "true"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "DEBUG_MODE", "false"
            signingConfig signingConfigs.production
        }

        debug {
            buildConfigField "boolean", "DEBUG_MODE", "true"
            signingConfig signingConfigs.test
        }
    }
    flavorDimensions "normal"
    productFlavors {
        //开发版
        develop {
            dimension "normal"
            versionName '1.1.0'
            versionCode 1
            applicationId 'com.gzr.gzrdemo'
            manifestPlaceholders = [
                    JPUSH_APPKEY      : "27abadd0760ccc7a3d11e890",
                    JPUSH_PKGNAME     : applicationId,
                    JPUSH_CHANNEL     : "developer-default",
                    Baidu_API_KEY: "C4D9FpiBBK414KiGal2809BqAfvCS1Vc",
                    APPLICATION_ID    : applicationId]
            signingConfig signingConfigs.test
        }
        //演示版
        staging {
            dimension "normal"
            applicationId 'com.gzr.gzrdemo'
            manifestPlaceholders = [
                    JPUSH_APPKEY      : "27abadd0760ccc7a3d11e890",
                    JPUSH_PKGNAME     : applicationId,
                    JPUSH_CHANNEL     : "developer-default",
                    Baidu_API_KEY: "C4D9FpiBBK414KiGal2809BqAfvCS1Vc",
                    APPLICATION_ID    : applicationId]
            versionName '1.1.0'
            versionCode 2
            signingConfig signingConfigs.test
        }
        //发布版
        production {
            dimension "normal"
            applicationId 'com.gzr.gzr'
            manifestPlaceholders = [
                    JPUSH_APPKEY      : "eb462c40c4949792ca6e7712",
                    JPUSH_PKGNAME     : applicationId,
                    JPUSH_CHANNEL     : "developer-default",
                    Baidu_API_KEY: "C4D9FpiBBK414KiGal2809BqAfvCS1Vc",
                    APPLICATION_ID    : applicationId]
            buildConfigField "boolean", "PRINT_LOG", "false"
            versionName '1.1.0'
            versionCode 1
            signingConfig signingConfigs.production
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.productFlavors[0].name.equals('develop')) {
                def fileName = "GZR_Test_" + releaseTime() + ".apk"
                outputFileName = fileName
            }
            if (variant.productFlavors[0].name.equals('staging')) {
                def fileName = "GZR_Stg_" + releaseTime() + ".apk"
                outputFileName = fileName
            }
            if (variant.productFlavors[0].name.equals('production')) {
                def fileName = "GZR_" + variant.productFlavors[0].versionName + ".apk"
                outputFileName = fileName
            }
        }
    }
    buildToolsVersion '28.0.3'

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28+'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    //日志框架控件
    implementation files('libs/log4j-1.2.17.jar')
    implementation files('libs/android-logging-log4j-1.0.3.jar')
    //本地数据库控件
    implementation 'org.litepal.android:java:3.0.0'
    //butterknife控件
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation files('libs/BaiduLBS_Android.jar')
}
